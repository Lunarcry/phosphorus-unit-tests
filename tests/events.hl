
/*
 * Unit tests for dynamically created Active Events.
 */


Stateful event is stateful
  create-event:unit-tests.tests.stateful-event
    _static:node:"count:int:0"
    set:x:/@_static/#?value
      +:x:/@_static/#?value
        _:int:1
    eval-x:x:/+
    return:Count {0}
      :x:/@_static/#?value
  unit-tests.tests.stateful-event
  unit-tests.tests.stateful-event
  unit-tests.tests.stateful-event
  if:x:/../*/unit-tests.tests.stateful-event/[0,1]?value
    !=:Count 1
    or:x:/../*/unit-tests.tests.stateful-event/[1,2]?value
      !=:Count 2
    or:x:/../*/unit-tests.tests.stateful-event/[2,3]?value
      !=:Count 3
    throw:Assert error, p5.events misbehaved
  p5.events.delete:unit-tests.tests.stateful-event


Event can return nodes
  create-event:unit-tests.tests.return-nodes
    return
      foo:bar
  unit-tests.tests.return-nodes
  if:x:/@unit-tests.tests.return-nodes
    !=:node:@"unit-tests.tests.return-nodes
  foo:bar"
    throw:Assert error, p5.events misbehaved


Delete event
  create-event:unit-tests.tests.event-2-delete
    return:foo
  unit-tests.tests.event-2-delete
  p5.events.delete:unit-tests.tests.event-2-delete
  unit-tests.tests.event-2-delete
  if:x:/../*/unit-tests.tests.event-2-delete/[0,1]?value
    !=:foo
    or:x:/../*/unit-tests.tests.event-2-delete/[1,2]?value
    throw:Assert error, p5.events misbehaved


Overwrite event
  create-event:unit-tests.tests.overwrite-event
    return:foo1
  unit-tests.tests.overwrite-event
  create-event:unit-tests.tests.overwrite-event
    return:foo2
  unit-tests.tests.overwrite-event
  if:x:/../*/unit-tests.tests.overwrite-event/[0,1]?value
    !=:foo1
    or:x:/../*/unit-tests.tests.overwrite-event/[1,2]?value
      !=:foo2
    throw:Assert error, p5.events misbehaved


Create protected event throws 1
  _success:bool:false
  try
    create-event:.foo
      foo
  catch
    set:x:/@_success?value
      src:boool:true
  if:x:/@_success?value
    not
    throw:Assert error, p5.events misbehaved


Create protected event throws 2
  _success:bool:false
  try
    create-event:_foo
      foo
  catch
    set:x:/@_success?value
      src:boool:true
  if:x:/@_success?value
    not
    throw:Assert error, p5.events misbehaved


Lambda injection into event is not possible
  create-event:unit-tests.tests.injection-test
    return
      foo:bar
  unit-tests.tests.injection-test
    throw:Foo bar
  if:x:/@unit-tests.tests.injection-test
    !=:node:@"unit-tests.tests.injection-test
  foo:bar"
    throw:Assert error, p5.events misbehaved


Event does not return [_arg] expression
  create-event:unit-tests.tests.no-return
    return
      foo:bar
  _data:foo
  unit-tests.tests.no-return:x:/../*/_data?value
  if:x:/@unit-tests.tests.no-return
    !=:node:@"unit-tests.tests.no-return
  foo:bar"
    throw:Assert error, p5.events misbehaved


Event does not return [_arg] constant
  create-event:unit-tests.tests.no-return-2
    return
      foo:bar
  unit-tests.tests.no-return-2:foo
  if:x:/@unit-tests.tests.no-return-2
    !=:node:@"unit-tests.tests.no-return-2
  foo:bar"
    throw:Assert error, p5.events misbehaved
